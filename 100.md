Polished Cream Narwhal

medium

# A silent overflow lets users have more voting power than allowed

## Summary

Because of a wrong check, the system can mint **uint256.max** tokens, but record that it had minted **uint120.max** only tokens. 

## Vulnerability Detail

On initialization, [**Distributor.sol**](https://github.com/sherlock-audit/2023-06-tokensoft/blob/main/contracts/contracts/claim/abstract/Distributor.sol#L54)  makes a check that  the passed totalAmount is less than uint120 max. However, the check uses **totalAmount** (the already casted number) instead of the uint256 **_totalAmount**.

```solidity
 function _initializeDistributionRecord(
    address beneficiary,
    uint256 _totalAmount
  ) internal virtual {
    uint120 totalAmount = uint120(_totalAmount);

    // Checks
    require(totalAmount <= type(uint120).max, 'Distributor: totalAmount > type(uint120).max');

    // Effects - note that the existing claimed quantity is re-used during re-initialization
    records[beneficiary] = DistributionRecord(true, totalAmount, records[beneficiary].claimed);
    emit InitializeDistributionRecord(beneficiary, totalAmount);
  }

```
  
   This is dangerous because [**AdvancedDistributor.sol**](https://github.com/sherlock-audit/2023-06-tokensoft/blob/main/contracts/contracts/claim/abstract/AdvancedDistributor.sol#L84) mints **totalAmount** of votes to a given user. It expects that the given value is a valid **uint120** number because of the check in **Distributor.sol** will handle that, but as we already saw, a revert will not happen.

```solidity
  function _initializeDistributionRecord(
    address beneficiary,
    uint256 totalAmount
  ) internal virtual override {
    super._initializeDistributionRecord(beneficiary, totalAmount);

    // add voting power through ERC20Votes extension
    _mint(beneficiary, tokensToVotes(totalAmount));
  }
```

## Impact
  Users get minted more voting power than allowed.

## Code Snippet

## Tool used

Manual Review

## Recommendation
Use the correct **_totalAmount** in the check: 

 ```solidity
 require(_totalAmount <= type(uint120).max, 'Distributor: totalAmount > type(uint120).max');
 ```