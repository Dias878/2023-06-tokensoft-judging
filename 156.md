Daring Basil Locust

high

# Satellite's use of `abi.encodePacked` makes protocol's cross-chain communication not viable

## Summary
The `Satellite` contract is a helper for relaying cross-chain claim requests. It wraps the `connext` bridge call by conveniently encoding a call and forwards it for relaying to the cross-chain distributor on another chain. However, it does not correctly encode the relayed data, so it's not a viable entry point for cross-chain communication.

## Vulnerability Detail
The `Satellite`'s `initiateClaim` [uses](https://github.com/sherlock-audit/2023-06-tokensoft/blob/main/contracts/contracts/claim/Satellite.sol#L95) the `abi.encodePacked` method to assemble the interchain call data; on the receiving side, `CrosschainMerkleDistributor`'s `xReceive` [uses](https://github.com/sherlock-audit/2023-06-tokensoft/blob/main/contracts/contracts/claim/abstract/CrosschainMerkleDistributor.sol#L72C22-L72C22) the `abi.decode` method to decode it. These two are incompatible, at the very least because `encodePacked` does not include the `proof.length` in the output, while `decode` expects it in its input. More formally, [the Solidity documentation of `encodePacked`](https://docs.soliditylang.org/en/latest/abi-spec.html#non-standard-packed-mode) explicitly mentions that `Since the encoding is ambiguous, there is no decoding function.`

## Impact
The vulnerability makes the cross-chain interoperability between the `Satellite` and the `CrosschainMerkleDistributor` contracts entirely non-viable. In general, the cross-chain nature of the audited protocol is compromised, and more specifically any user calling the `Satellite` `initiateClaim` function - which requires paying a fee for the cross-chain relaying - would end up losing the fees paid.

## Code Snippet
Bridge's caller (`xcall`) side ([link](https://github.com/sherlock-audit/2023-06-tokensoft/blob/main/contracts/contracts/claim/Satellite.sol#L95)):
```Solidity
    bytes32 transferId = connext.xcall{ value: msg.value }(
      _distributorDomain, // destination domain
      address(distributor), // to
      address(0), // asset
      address(0), // delegate, only required for self-execution + slippage
      0, // total
      0, // slippage
      abi.encodePacked(msg.sender, _domain, total, proof) // data
    );
```
Bridge's callee (`xReceive`) side ([link](https://github.com/sherlock-audit/2023-06-tokensoft/blob/main/contracts/contracts/claim/abstract/CrosschainMerkleDistributor.sol#L72C22-L72C22)):
```Solidity
  function xReceive(
    bytes32, // _transferId,
    uint256, // _amount,
    address, // _asset,
    address, // _originSender,
    uint32, // _origin,
    bytes calldata _callData
  ) external onlyConnext returns (bytes memory) {
    // Decode the data
    (address beneficiary, uint32 beneficiaryDomain, uint256 totalAmount, bytes32[] memory proof) = abi
      .decode(_callData, (address, uint32, uint256, bytes32[]));
```

A quick PoC can be done in Solidity to prove that `abi.encodePacked` and `abi.decode` aren't compatible, specifically when used in this context:
```Solidity
    // abi.encodePacked(msg.sender, _domain, total, proof) // data

    function testIncompatible() public {
        address sender = address(this);
        uint256 total = 123;
        uint32 domain = 456;
        bytes32[] memory proof = new bytes32[](2);
        proof[0] = bytes32(uint256(789));
        proof[1] = bytes32(uint256(987));

        bytes memory encoded = abi.encodePacked(sender, domain, total, proof);
        vm.expectRevert(); // ðŸ¤¯ boom!
        abi.decode(encoded, (address, uint32, uint256, bytes32[]));
    }

    function testCompatible() public {
        address sender = address(this);
        uint256 total = 123;
        uint32 domain = 456;
        bytes32[] memory proof = new bytes32[](2);
        proof[0] = bytes32(uint256(789));
        proof[1] = bytes32(uint256(987));

        bytes memory encoded = abi.encode(sender, domain, total, proof);
        // all is fine ðŸ˜Ž
        (address dSender, uint32 dDomain, uint256 dTotal, bytes32[] memory dProof) = abi.decode(encoded, (address, uint32, uint256, bytes32[]));
        assertEq(sender, dSender);
        assertEq(domain, dDomain);
        assertEq(total, dTotal);
        assertEq(2, dProof.length);
        assertEq(proof[0], dProof[0]);
        assertEq(proof[1], dProof[1]);
    }
```

## Tool used

Manual Review

## Recommendation
Change `Satellite.sol` to use `abi.encode` instead of `abi.encodePacked`.