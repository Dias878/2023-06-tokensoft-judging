Cheesy Laurel Beaver

medium

# Contracts on non-airdrop chains may not claim airdrops

## Summary

According to the code notes, anyone can claim an airdrop on behalf of a beneficiary via connext on a non-airdrop chain, and the airdrop token will then connext forward to the corresponding beneficiaryDomain chain.
However, the specific code implementation only allows `msg.sender` to receive his own airdrop, and cannot claim airdrop for others. Therefore, if a contract does not implement the call to `Satellite.initiateClaim`, it will not be able to claim airdrop, which is not in line with expectations.

## Vulnerability Detail

```solidity
  /**
   * @notice Used for cross-chain claims via Satellite, which triggers claims through Connext.
   * @dev This method is only callable by Connext, but anyone on any other Connext domain can
   * trigger this method call on behalf of a beneficiary. Claimed funds will always be sent to
   * the beneficiary address and beneficiary domain set in the merkle proof.
   * @param _callData Calldata from origin initiator (Satellite). Should include proof, leaf information, and recipient
   * information
   */
  function xReceive(
    bytes32, // _transferId,
    uint256, // _amount,
    address, // _asset,
    address, // _originSender,
    uint32, // _origin,
    bytes calldata _callData
  ) external onlyConnext returns (bytes memory) {
    // Decode the data
    (address beneficiary, uint32 beneficiaryDomain, uint256 totalAmount, bytes32[] memory proof) = abi
      .decode(_callData, (address, uint32, uint256, bytes32[]));
    _verifyMembership(_getLeaf(beneficiary, totalAmount, beneficiaryDomain), proof);

    // effects
    uint256 claimedAmount =  _executeClaim(beneficiary, totalAmount);

    // interactions
    _settleClaim(beneficiary, beneficiary, beneficiaryDomain, claimedAmount);

    return bytes('');
  }


  function initiateClaim(uint256 total, bytes32[] calldata proof) public payable {
    // load values into memory to reduce sloads
    uint32 _distributorDomain = distributorDomain;
    uint32 _domain = domain;

    // Verify the proof before sending cross-chain as a cost + time saving step
    _verifyMembership(keccak256(abi.encodePacked(msg.sender, total, _domain)), proof);

    // Send claim to distributor via crosschain call
    bytes32 transferId = connext.xcall{ value: msg.value }(
      _distributorDomain, // destination domain
      address(distributor), // to
      address(0), // asset
      address(0), // delegate, only required for self-execution + slippage
      0, // total
      0, // slippage
      abi.encodePacked(msg.sender, _domain, total, proof) // data
    );

    // Emit event
    emit ClaimInitiated(transferId, msg.sender, total);
  }
```

According to the note: This method is only callable by Connext, but anyone on any other Connext domain can trigger this method call on behalf of a beneficiary. But as we can see in initiateClaim, only the `msg.sender` can be the `beneficiary`.
The other two methods in the contract, `claimByMerkleProof` and `claimBySignature`, can only claim the airdrop of the current domain and EOA, but cannot claim the airdrop of the `beneficiaryDomain` chain. Therefore, contracts that do not implement corresponding methods cannot receive airdrops, which does not meet expectations.

## Impact

If a contract does not implement the call to `Satellite.initiateClaim`, it will not be able to claim airdrop.

## Code Snippet

- https://github.com/sherlock-audit/2023-06-tokensoft/blob/1f58ddb066ab383c416cfcbf95c9902683506e96/contracts/contracts/claim/Satellite.sol#L95
- https://github.com/sherlock-audit/2023-06-tokensoft/blob/1f58ddb066ab383c416cfcbf95c9902683506e96/contracts/contracts/claim/abstract/CrosschainMerkleDistributor.sol#L57-L59

## Tool used

Manual Review

## Recommendation

The beneficiary address should be passed in the initiateClaim instead of using msg.sender
