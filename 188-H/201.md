Ancient Coal Caribou

medium

# Airdrop amount superior than type(uint256).max will result in stucked voting tokens

## Summary
If the airdrop amount is more than `type(uint120).max` the transaction will not revert as expected but will result in voting tokens being stucked.

## Vulnerability Detail
The check that `totalAmount` is not greater than `type(uint120).max` is made on the local variable `totalAmount` that is already downcasted to `uint120` instead of the `uint256 _totalAmount` argument. Hence, the test will always pass.

## Impact
An airdrop of more than `type(uint120).max` tokens will not revert as expected. Instead, the `uint120(_totalAmount)` tokens will be distributed and the total amount will be mint as voting tokens in `AdvancedDistributor.sol > _initializeDistributionRecord()`.

```solidity
File claim/abstract/AdvencedDistributor.sol

L77:	function _initializeDistributionRecord(
    address beneficiary,
    uint256 totalAmount
  ) internal virtual override {
    super._initializeDistributionRecord(beneficiary, totalAmount);

    // add voting power through ERC20Votes extension
    //@audit - tokensToVotes(totalAmount) is minted
    _mint(beneficiary, tokensToVotes(totalAmount));
  }
```
https://github.com/SoftDAO/contracts/blob/291df55ddb0dbf53c6ed4d5b7432db0c357ca4d3/contracts/claim/abstract/AdvancedDistributor.sol#L84

Then, once claiming the tokens only `uint120(_totalAmount)` voting tokens will be burned is `AdvancedDistributor.sol > _executeClaim()`

```solidity
File claim/abstract/AdvencedDistributor.sol

L87:	function _executeClaim(
    address beneficiary,
    uint256 totalAmount
  ) internal virtual override returns (uint256 _claimed) {
    _claimed = super._executeClaim(beneficiary, totalAmount);

    // reduce voting power through ERC20Votes extension
    //@audit - only tokensToVotes(_claimed) amount is burned, i.e. uint120(totalAmount)
    _burn(beneficiary, tokensToVotes(_claimed));
  }
```
https://github.com/SoftDAO/contracts/blob/291df55ddb0dbf53c6ed4d5b7432db0c357ca4d3/contracts/claim/abstract/AdvancedDistributor.sol#L94

This results in `totalAmount - uint120(totalAmount)` voting tokens being stucked and a wrong tracking of voting power.

## Code Snippet

```solidity
File claim/Distributor.sol

L47:	function _initializeDistributionRecord(
    address beneficiary,
    uint256 _totalAmount
  ) internal virtual {
    uint120 totalAmount = uint120(_totalAmount); // @audit - dangerous downcasting

    // Checks
    //@audit - this check will always pass because the check is on local variable instead of argument
    require(totalAmount <= type(uint120).max, 'Distributor: totalAmount > type(uint120).max');

    // Effects - note that the existing claimed quantity is re-used during re-initialization
    records[beneficiary] = DistributionRecord(true, totalAmount, records[beneficiary].claimed);
    emit InitializeDistributionRecord(beneficiary, totalAmount);
  }
```
https://github.com/SoftDAO/contracts/blob/291df55ddb0dbf53c6ed4d5b7432db0c357ca4d3/contracts/claim/abstract/Distributor.sol#L54

## Tool used

Manual Review

## Recommendation

Check the argument instead of the local variable:
```diff
File claim/abstract/Distributor.sol

L47:	function _initializeDistributionRecord(
    address beneficiary,
    uint256 _totalAmount
  ) internal virtual {
    uint120 totalAmount = uint120(_totalAmount); // @audit - dangerous downcasting

    // Checks
-    require(totalAmount <= type(uint120).max, 'Distributor: totalAmount > type(uint120).max');
+    require(_totalAmount <= type(uint120).max, 'Distributor: totalAmount > type(uint120).max');

    // Effects - note that the existing claimed quantity is re-used during re-initialization
    records[beneficiary] = DistributionRecord(true, totalAmount, records[beneficiary].claimed);
    emit InitializeDistributionRecord(beneficiary, totalAmount);
  }
```