Cold Cyan Gecko

high

# Users can mint more vote tokens than intended

## Summary
`initializeDistributionRecord` can be called several times to mint a very large amount of vote tokens.

## Vulnerability Detail
`initializeDistributionRecord` allows users to permisionlessly initialize the distribution record:

[CrosschainMerkleDistributor.sol#L46-L53](https://github.com/sherlock-audit/2023-06-tokensoft/blob/main/contracts/contracts/claim/abstract/CrosschainMerkleDistributor.sol#L46-L53)
```solidity
  function initializeDistributionRecord(
    uint32 _domain, // the domain of the beneficiary
    address _beneficiary, // the address that will receive tokens
    uint256 _amount, // the total claimable by this beneficiary
    bytes32[] calldata merkleProof
  ) external validMerkleProof(_getLeaf(_beneficiary, _amount, _domain), merkleProof) {
    _initializeDistributionRecord(_beneficiary, _amount);
  }
```

[AdvancedDistributor.sol#L77-L85](https://github.com/sherlock-audit/2023-06-tokensoft/blob/main/contracts/contracts/claim/abstract/AdvancedDistributor.sol#L77-L85)
```solidity
  function _initializeDistributionRecord(
    address beneficiary,
    uint256 totalAmount
  ) internal virtual override {
    super._initializeDistributionRecord(beneficiary, totalAmount);


    // add voting power through ERC20Votes extension
    _mint(beneficiary, tokensToVotes(totalAmount));
  }
```

[Distributor.sol#L47-L59](https://github.com/sherlock-audit/2023-06-tokensoft/blob/main/contracts/contracts/claim/abstract/Distributor.sol#L47-L59)
```solidity
function _initializeDistributionRecord(
    address beneficiary,
    uint256 _totalAmount
  ) internal virtual {
    uint120 totalAmount = uint120(_totalAmount);


    // Checks
    require(totalAmount <= type(uint120).max, 'Distributor: totalAmount > type(uint120).max');


    // Effects - note that the existing claimed quantity is re-used during re-initialization
    records[beneficiary] = DistributionRecord(true, totalAmount, records[beneficiary].claimed);
    emit InitializeDistributionRecord(beneficiary, totalAmount);
}
```

However, as you can see in the provided code snippets, there is no mechanism that prevents users to call `initializeDistributionRecord` again and mint the same amount of vote tokens according to their `_amount` validated via merkle proof. The `records` mapping is simply initialized again and new vote tokens are minted each time.

## Impact
Users can mint arbitrary amount of tokens which will lead to unfair governance results.

## Code Snippet
See above.

## Tool used

Manual Review

## Recommendation
Consider checking if the `records` mapping has already been initialized for this user.